# DEV trust: only deploy/* branches
data "aws_iam_policy_document" "dev_trust" {
  statement {
    sid     = "AllowGitHubOIDC"
    effect  = "Allow"
    actions = ["sts:AssumeRoleWithWebIdentity"]

    principals {
      type        = "Federated"
      identifiers = [aws_iam_openid_connect_provider.github.arn]
    }

    condition {
      test     = "StringLike"
      variable = "token.actions.githubusercontent.com:sub"
      values = [
        "repo:egobb/order-tracking:environment:dev",         # <- si el job usa environment: dev
        "repo:egobb/order-tracking:ref:refs/heads/feature/infra-aws",
        "repo:egobb/order-tracking:ref:refs/heads/deploy/*", # <- si disparas desde deploy/*
        "repo:egobb/order-tracking:ref:refs/heads/develop",  # opcional
      ]
    }

    condition {
      test     = "StringEquals"
      variable = "token.actions.githubusercontent.com:aud"
      values   = ["sts.amazonaws.com"]
    }
  }
}

# PROD trust: only tags v*
data "aws_iam_policy_document" "prod_trust" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRoleWithWebIdentity"]

    principals {
      type        = "Federated"
      identifiers = [aws_iam_openid_connect_provider.github.arn]
    }

    condition {
      test     = "StringLike"
      variable = "token.actions.githubusercontent.com:sub"
      values   = ["repo:egobb/order-tracking:ref:refs/tags/v*"]
    }

    condition {
      test     = "StringEquals"
      variable = "token.actions.githubusercontent.com:aud"
      values   = ["sts.amazonaws.com"]
    }
  }
}